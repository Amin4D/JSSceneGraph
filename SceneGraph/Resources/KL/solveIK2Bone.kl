
//
// Copyright 2010-2011 Fabric Technologies Inc. All rights reserved.
//

operator solveIK2Bone(
  io Xfo pose[],
  io Bone bones[],
  io Integer boneA,
  io Integer boneB,
  io Integer targetParent,
  io Integer upvectorParent,
  io Xfo local,
  io Xfo target,
  io Xfo upvector
) {

  if (bones[boneA].parent != - 1) {
    pose[boneA] = local * pose[bones[boneA].parent];
  }

  pose[boneB] = bones[boneB].referenceLocalPose * pose[boneA];

  // compute the manipulator positions
  var Vec3 targetPos = pose[targetParent].transform(target.tr);
  var Vec3 upvectorPos = pose[upvectorParent].transform(upvector.tr);

  // compute the sides of the triangles and their lengths
  var Vec3 sideA = upvectorPos - (pose[boneA].tr + targetPos) * 0.5;
  var Vec3 sideC = targetPos - pose[boneA].tr;
  var Scalar lengthA = bones[boneA].length;
  var Scalar lengthB = bones[boneB].length;
  var Scalar lengthC = sideC.norm();

  // compute the direction of the first bone
  var Vec3 xAxis;
  var Vec3 zAxis = sideA.cross(sideC).unit();
  if (lengthC < lengthA + lengthB) {
    var Scalar angle = - acos((lengthA * lengthA + lengthC * lengthC - lengthB * lengthB) / (2.0 * lengthA * lengthC));
    var Quat q = axisAndAngleToQuat(zAxis, angle);
    xAxis = q.transform(sideC).unit();
  } else {
    xAxis = sideC.unit();
  }
    
  // orient bones
  var Vec3 yAxis = upvectorPos - targetPos;
  pose[boneA].ori = directionToQuat(xAxis,yAxis);
  pose[boneB].tr = pose[boneA].tr + xAxis * bones[boneA].length;
  xAxis = targetPos - pose[boneB].tr;
  pose[boneB].ori = directionToQuat(xAxis,yAxis);
}

operator solveInvIK2Bone(
  io Xfo pose[],
  io Bone bones[],
  io Integer boneA,
  io Integer boneB,
  io Integer targetParent,
  io Integer upvectorParent,
  io Xfo local,
  io Xfo target,
  io Xfo upvector
) {
  local = pose[bones[boneA].parent].invProject(pose[boneA]);
  target = pose[boneB];
  target.tr = target.transform(Vec3(bones[boneB].length, 0.0, 0.0));
  upvector = pose[boneB];
  upvector.tr = (target.tr + pose[boneA].tr) * 0.5;
  upvector.tr = (pose[boneB].tr -  upvector.tr).unit() * bones[boneA].length + pose[boneB].tr;
  
  // project into parent spaces
  target = pose[targetParent].invProject(target);
  upvector = pose[upvectorParent].invProject(upvector);
}