
//
// Copyright 2010-2011 Fabric Technologies Inc. All rights reserved.
//


function OGLTexture2D.configure(
  Size width,
  Size height,
  Data pixels,
  Size nbPixels,
  Size glSrcDataType,
  io Integer textureUnit
) {
//  report("OGLTexture2D.bind:" + textureUnit + " nbPixels:" + nbPixels);
//  report("createTextureFromImage  bufferID:"+self.bufferID + " textureUnit:"+textureUnit  + " nbPixels:"+nbPixels );
  if(nbPixels > 0){
    var Size buffers[];
    buffers.resize(1);
    glGenTextures(1, buffers);
    self.bufferID = buffers[0];
    glActiveTexture(GL_TEXTURE0 + textureUnit);
    glBindTexture(GL_TEXTURE_2D, self.bufferID);

    // glTexEnv( GL_TEXTURE_ENV, GL_TEXTURE_ENV_MODE, GL_MODULATE ); //set texture environment parameters

    //Generate the texture
    glPixelStorei(GL_UNPACK_ALIGNMENT, 1);
    if (nbPixels == (width * height))
      glTexImage2D(GL_TEXTURE_2D, 0, self.glInternalFormat, width, height, 0, self.glFormat, self.glType, pixels);

    glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_MIN_FILTER, GL_LINEAR);
    glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_MAG_FILTER, GL_LINEAR);

    glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_WRAP_S, GL_REPEAT);
    glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_WRAP_T, GL_REPEAT);
  }
}


function OGLTexture2D.bind(io Integer textureUnit){
  glActiveTexture(GL_TEXTURE0 + textureUnit);
  glBindTexture(GL_TEXTURE_2D, self.bufferID);
}


function OGLTexture2D.bindImageLDR(
  io Size width,
  io Size height,
  io RGBA bitmap[],
  io Integer textureUnit
) {
  if (self.bufferID == 0) {
    self.configure(width, height, bitmap.data(), bitmap.size(), GL_RGBA, textureUnit);
  }
  self.bind(textureUnit);
}


function OGLTexture2D.bindImageHDR(
  io Size width,
  io Size height,
  io Color bitmap[],
  io Integer textureUnit
) {
  if (self.bufferID == 0) {
    self.configure(width, height, bitmap.data(), bitmap.size(), GL_RGBA16F_ARB, textureUnit);
  }
  self.bind(textureUnit);
}


function OGLTexture2D.genTexture(
  Size width,
  Size height
) {
//  report("OGLTexture2D.genTexture width:" + width + " height:" + height);
  
  var Data NULL;
  /////////////////////////////////////////////////////
  // Color Texture
  Size bufferIDs[];
  bufferIDs.push(Size(0));
  glGenTextures(1, bufferIDs);
  self.bufferID = bufferIDs[0];
  
  glBindTexture(GL_TEXTURE_2D, self.bufferID);
  glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_WRAP_S, GL_CLAMP);
  glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_WRAP_T, GL_CLAMP);
  
  if(self.glInternalFormat == GL_DEPTH_COMPONENT){
    glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_MIN_FILTER, GL_LINEAR);
    glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_MAG_FILTER, GL_LINEAR);
    
    glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_COMPARE_MODE, GL_COMPARE_R_TO_TEXTURE);
    glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_COMPARE_FUNC, GL_LEQUAL);
    glTexParameteri(GL_TEXTURE_2D, GL_DEPTH_TEXTURE_MODE, GL_INTENSITY);
  }else{
    glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_MIN_FILTER, GL_NEAREST);
    glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_MAG_FILTER, GL_NEAREST);
  }
  
  //NULL means reserve texture memory, but texels are undefined
  glTexImage2D(GL_TEXTURE_2D, 0, self.glInternalFormat, width, height, 0, self.glFormat, self.glType, NULL);
  
  self.nbPixels = width * height;
}


