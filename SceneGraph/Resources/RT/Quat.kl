
//
// Copyright 2010-2011 Fabric Technologies Inc. All rights reserved.
//

function Quat() {
  self.v = Vec3(0.0, 0.0, 0.0);
  self.w = 1.0;
}

function Quat.identity() {
  self.v = Vec3(0.0, 0.0, 0.0);
  self.w = 1.0;
}


function Quat(in Scalar w, in Vec3 v) {
  self.v = v;
  self.w = w;
}

function Quat(in Euler e) {
  var Scalar ti, tj, tk;
  ti = e.x * 0.5;
  tj = e.ro.isReversed() ? - e.y * 0.5 : e.y * 0.5;
  tk = e.z * 0.5;
  var Scalar ci = cos(ti), cj = cos(tj), ck = cos(tk);
  var Scalar si = sin(ti), sj = sin(tj), sk = sin(tk);
  var Scalar cc = ci * ck, cs = ci * sk, sc = si * ck, ss = si * sk;
  var Scalar ai, aj, ak;
  ai = cj * sc - sj * cs;
  aj = cj * ss + sj * cc;
  ak = cj * cs - sj * sc;

  if (e.ro.isReversed()) {
    aj = - aj;
  }
  self.w = cj * cc + sj * ss;

  if (e.ro.isXYZ()) {
    self.v.x = ai; self.v.y = aj; self.v.z = ak;
  }
  else if (e.ro.isYZX()) {
    self.v.x = aj; self.v.y = ak; self.v.z = ai;
  }
  else if (e.ro.isZXY()) {
    self.v.x = ak; self.v.y = ai; self.v.z = aj;
  }
  else if (e.ro.isXZY()) {
    self.v.x = ai; self.v.y = ak; self.v.z = aj;
  }
  else if (e.ro.isZYX()) {
    self.v.x = ak; self.v.y = aj; self.v.z = ai;
  }
  else if (e.ro.isYXZ()) {
    self.v.x = aj; self.v.y = ai; self.v.z = ak;
  }
}

function Quat + (in Quat a, in Quat b) {
  return Quat(a.w + b.w, a.v + b.v);
}

function Quat - (in Quat a, in Quat b) {
  return Quat(a.w - b.w, a.v - b.v);
}

function Quat * (in Quat a, in Quat b) {
  return Quat(a.w * b.w - a.v.dot(b.v), a.v.cross(b.v) + (a.w * b.v) + (a.v * b.w));
}

function Quat * (in Scalar a, in Quat b) {
  return Quat(a * b.w, a * b.v);
}

function Quat * (in Quat a, in Scalar b) {
  return Quat(a.w * b, a.v * b);
}

function Quat / (in Quat a, in Quat b) {
  return Quat(a.w * b.w + a.v.dot(b.v), (a.v * b.w) - (a.w * b.v) - a.v.cross(b.v));
}

function Scalar Quat.normSq() {
  return self.w * self.w + self.v.normSq();
}

function Scalar Quat.norm() {
  return sqrt(self.normSq());
}

function Quat Quat.unit() {
  var Scalar l = self.norm();
  return Quat(self.w / l, self.v / l);
}

function Quat.setUnit() {
  self = self.unit();
}

function Quat.normalize() {
  self = self.unit();
}

function Quat Quat.conjugate() {
  return Quat(self.w, self.v.neg());
}

function Quat Quat.inv() {
  return self.unit().conjugate();
}

function Scalar Quat.dot(in Quat other) {
  return self.w * other.w + self.v.dot(other.v);
}

function Quat.makeClosest(in Quat other) {
  if(self.dot(other) < 0.0){
    self = Quat(-self.w, self.v.neg());;
  }
}


// Note: a and b should be unit Quaternions
function Quat slerp(in Quat a, in Quat b, in Scalar t) {
  var Quat r;

  var Scalar cosHalfTheta = a.dot(b);

  if (abs(cosHalfTheta) < 1.0) {
    var Scalar halfTheta = acos(cosHalfTheta);
    var Scalar sinHalfTheta = sqrt(1.0 - cosHalfTheta * cosHalfTheta);

    if (abs(sinHalfTheta) < 0.00001) {
      r = Quat((a.w + b.w) * 0.5, (a.v + b.v) * 0.5);
    }
    else {
      var Scalar rA = sin((1 - t) * halfTheta) / sinHalfTheta;
      var Scalar rB = sin(t * halfTheta) / sinHalfTheta;

      r = Quat(a.w * rA + b.w * rB, a.v * rA + b.v * rB);
    }
  }
  else {
    // They're the same
    r = a;
  }

  return r.unit();
}

function Quat axisAndAngleToQuat(in Vec3 axis, in Scalar angle) {
  var Scalar halfAngle = angle / 2.0;
  return Quat(cos(halfAngle), sin(halfAngle) * axis.unit());
}

// NB. Function taken from the 'Game Programming Gems' article 'The Shortest Arc Quat' by Stan Melax
// NB. Both vectors must be normalized.
function Quat makeQuatFrom2Vectors(in Vec3 vec1, in Vec3 vec2) {
  Quat q;
  Scalar val = vec1.dot(vec2) + 1.0;
  if (val <= 0.001) {
    // the vectors pointed in opposite directions.
    // creating the quaterion is impossible
    return q;
  }
  val = sqrt(val * 2.0);
  q.v = vec1.cross(vec2);
  q.v /= val;
  q.w = val / 2.0;
  return q;
}

// Method for obtaining axes directly from a quaternion
function Vec3 Quat.getXaxis() {
  Vec3 temp;
  Scalar xy = self.v.x * self.v.y; Scalar xz = self.v.x * self.v.z;
  Scalar yy = self.v.y * self.v.y; Scalar yw = self.v.y * self.w;
  Scalar zz = self.v.z * self.v.z; Scalar zw = self.v.z * self.w;

  temp.x = 1.0 - 2.0 * (zz + yy);
  temp.y = 2.0 * (xy + zw);
  temp.z = 2.0 * (xz - yw);
  return temp;
}

function Vec3 Quat.getYaxis() {
  Vec3 temp;
  Scalar xx = self.v.x * self.v.x; Scalar xy = self.v.x * self.v.y; Scalar xw = self.v.x * self.w;
  Scalar yz = self.v.y * self.v.z;
  Scalar zz = self.v.z * self.v.z; Scalar zw = self.v.z * self.w;

  temp.x = 2.0 * (xy - zw);
  temp.y = 1.0 - 2.0 * (zz + xx);
  temp.z = 2.0 * (yz + xw);
  return temp;
}

function Vec3 Quat.getZaxis() {
  Vec3 temp;
  Scalar xx = self.v.x * self.v.x; Scalar xz = self.v.x * self.v.z; Scalar xw = self.v.x * self.w;

  Scalar yy = self.v.y * self.v.y; Scalar yz = self.v.y * self.v.z; Scalar yw = self.v.y * self.w;

  temp.x = 2.0 * (yw + xz);
  temp.y = 2.0 * (yz - xw);
  temp.z = 1.0 - 2.0 * (yy + xx);
  return temp;
}

function Mat33 Quat.makeMatrix() {
  Mat33 temp;

  Scalar xx = self.v.x * self.v.x; Scalar xy = self.v.x * self.v.y; Scalar xz = self.v.x * self.v.z; Scalar xw = self.v.x * self.w;
  Scalar yy = self.v.y * self.v.y; Scalar yz = self.v.y * self.v.z; Scalar yw = self.v.y * self.w;
  Scalar zz = self.v.z * self.v.z; Scalar zw = self.v.z * self.w;

  temp.row0.x = 1.0 - 2.0 * (zz + yy);
  temp.row1.x = 2.0 * (xy + zw);
  temp.row2.x = 2.0 * (xz - yw);

  temp.row0.y = 2.0 * (xy - zw);
  temp.row1.y = 1.0 - 2.0 * (zz + xx);
  temp.row2.y = 2.0 * (yz + xw);

  temp.row0.z = 2.0 * (yw + xz);
  temp.row1.z = 2.0 * (yz - xw);
  temp.row2.z = 1.0 - 2.0 * (yy + xx);

  return temp;
}

function Vec3 Quat.transform(in Vec3 v) {
  var Quat vq = Quat(0.0, v);
  var Quat pq = self * vq * self.conjugate();
  return pq.v;
}

// TODO: Remove the function above. 'transform' is too ambiguous,
// and inconsistent with our JS library.
function Vec3 Quat.rotateVector(in Vec3 v) {
  var Quat vq = Quat(0.0, v);
  var Quat pq = self * vq * self.conjugate();
  return pq.v;
}

function Quat(in Mat33 mat) {
  var Scalar trace = mat.row0.x + mat.row1.y + mat.row2.z;
  if (trace > 0) {
    var Scalar s = 0.5 / sqrt(trace + 1.0);
    self.w = 0.25 / s;
    self.v.x = (mat.row2.y - mat.row1.z) * s;
    self.v.y = (mat.row0.z - mat.row2.x) * s;
    self.v.z = (mat.row1.x - mat.row0.y) * s;
  }else {
    if (mat.row0.x > mat.row1.y && mat.row0.x > mat.row2.z) {
      var Scalar s = 2.0 * sqrt(1.0 + mat.row0.x - mat.row1.y - mat.row2.z);
      self.w = (mat.row2.y - mat.row1.z) / s;
      self.v.x = 0.25 * s;
      self.v.y = (mat.row0.y + mat.row1.x) / s;
      self.v.z = (mat.row0.z + mat.row2.x) / s;
    }else if (mat.row1.y > mat.row2.z) {
      var Scalar s = 2.0 * sqrt(1.0 + mat.row1.y - mat.row0.x - mat.row2.z);
      self.w = (mat.row0.z - mat.row2.x) / s;
      self.v.x = (mat.row0.y + mat.row1.x) / s;
      self.v.y = 0.25 * s;
      self.v.z = (mat.row1.z + mat.row2.y) / s;
    }else {
      var Scalar s = 2.0 * sqrt(1.0 + mat.row2.z - mat.row0.x - mat.row1.y);
      self.w = (mat.row1.x - mat.row0.y) / s;
      self.v.x = (mat.row0.z + mat.row2.x) / s;
      self.v.y = (mat.row1.z + mat.row2.y) / s;
      self.v.z = 0.25 * s;
    }
  }
  self.setUnit();
}



function Quat Quat.lerp(in Quat other, in Scalar t) {
  Quat q;
  q.v = self.v.lerp(other.v, t);
  q.w = self.w + ((other.w - self.w) * t);
  return q;
}


